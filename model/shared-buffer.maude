load prelude .
load model-checker.maude .
load full-maude .

(omod GLOBAL is
    including NAT .
    including LIST{Nat} .
    sorts NextStr Str .
    subsort NextStr < Configuration .
    subsort Nat < Str .

    op next_ : Str       -> NextStr .
    op max   : List{Nat} -> Nat .

    var N : Nat .
    var L : List{Nat} .

    eq max(N)   = N .
    eq max(N L) = if N > max(L) then N else max(L) fi .
endom)

(omod MSG is
    including GLOBAL .
    including STRING .

    sorts MsgContent .
    subsort String < Oid .

    op ack : -> MsgContent [ctor] .
    op ins : Str Nat Nat -> MsgContent [ctor] .
    op del : Nat Nat Nat -> MsgContent [ctor] .

    msg _from_to_ : MsgContent Oid Oid -> Msg .
endom)

(omod QUEUE is
    including MSG .
    sort MsgQueue .
    subsort Msg < MsgQueue .

    op empty : -> MsgQueue [ctor] .
    op _~_   : MsgQueue MsgQueue -> MsgQueue [ctor assoc id: empty] .
endom)

(omod SERVER is
    including QUEUE .
    including SET{Oid} .

    class Server | clients : Set{Oid} .
endom)

(omod CLIENT is
    including SERVER .

    sort Content .
    subsort List{Nat} < Content .

    op ins_at_in_ : Str    Nat Content -> Content .
    op del_at_in_ : Nat    Nat Content -> Content .
    op receive__  : MsgContent Content -> Content .

    class Client | content : Content, seqNo : Nat, out : MsgQueue, in : MsgQueue .

    var C      : Content .
    var S      : Str .
    vars I M N : Nat .

    eq ins S at 0 in C   = S C .
    eq ins S at s I in C = head(C) (ins S at I in tail(C)) .

    *** Assuming N is 1, change this later?
    eq del 1 at 0 in C   = tail(C) .
    eq del 1 at s I in C = head(C) (del 1 at I in tail(C)) .

    eq receive ins(S,I,N) C = ins S at I in C .
    eq receive del(M,I,N) C = del M at I in C .
endom)

(omod SERVER-RULES is
    including CLIENT .

    sort Multicast .
    subsort Multicast < Configuration .

    op multicast_from_to_ : MsgContent Oid Set{Oid} -> Multicast .

    vars O O' : Oid .
    var  M    : MsgContent .
    var  Q    : MsgQueue .
    var  CS   : Set{Oid} .

    rl [recv] :
        < O  : Client | out : Q ~ (M from O to O') >
        < O' : Server | clients : CS >
      =>
        < O  : Client | out : Q >
        < O' : Server | clients : CS >
        multicast M from O' to CS .

    eq < O : Client | in : Q > multicast M from O' to (O, CS) =
        < O : Client | in : (M from O' to O) ~ Q > multicast M from O' to CS .

    eq multicast M from O' to empty = none .
endom)

(omod CLIENT-RULES is
    including SERVER-RULES .

    vars O O'   : Oid .
    vars C C'   : Content .
    var S       : Str .
    var N       : Nat .
    var Q       : MsgQueue .

    rl [user-inserts] :
        < O  : Client | content : C C', seqNo : N, out : Q >
        < O' : Server | >
        next S
      =>
        < O  : Client | content : C C', seqNo : s N, out :
                        (ins(S, size(C), N) from O to O') ~ Q >
        < O' : Server | >
        next s S .

    rl [user-deletes] :
        < O  : Client | content : C S C', seqNo : N, out : Q >
        < O' : Server | >
      =>
        < O  : Client | seqNo : s N , out :
                        (del(1, size(C), N) from O to O') ~ Q >
        < O' : Server | > .
endom)

(omod TEST is
    including CLIENT-RULES .
    subsort String < Oid .

    op init : Content   -> Configuration .   --- a suitable initial state

    var C : Content .

    eq init(C) =
        < "S"  : Server | clients : "C1", "C2" >
        < "C1" : Client | content : C, seqNo : 0, out : empty, in : empty >
        < "C2" : Client | content : C, seqNo : 0, out : empty, in : empty >
        next if C == nil then 0 else s(max(C)) fi .
endom)

*** Commands:

*** (search[1] init(nil) =>* < "C1" : Client | content : 3 0 > C:Configuration .)
